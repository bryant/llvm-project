import("//llvm/utils/TableGen/tablegen.gni")

tablegen("AMDGPUGenAsmWriter") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-asm-writer" ]
  td_file = "../AMDGPU.td"
}

tablegen("AMDGPUGenMCCodeEmitter") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-emitter" ]
  td_file = "../AMDGPU.td"
}

tablegen("R600GenAsmWriter") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-asm-writer" ]
  td_file = "../R600.td"
}

tablegen("R600GenMCCodeEmitter") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-emitter" ]
  td_file = "../R600.td"
}

static_library("MCTargetDesc") {
  output_name = "LLVMAMDGPUDesc"

  deps = [
    "//llvm/lib/BinaryFormat",
    "//llvm/lib/IR",
    "//llvm/lib/MC",
    "//llvm/lib/Support",
    "../TargetInfo",
    "../Utils",

    ":AMDGPUGenAsmWriter",
    ":AMDGPUGenMCCodeEmitter",
    "../:R600GenRegisterInfo",
    ":R600GenAsmWriter",
    ":R600GenMCCodeEmitter",
  ]

  include_dirs = [ ".", ".." ]

  sources = [
    "AMDGPUAsmBackend.cpp",
    "AMDGPUELFObjectWriter.cpp",
    "AMDGPUELFStreamer.cpp",
    "AMDGPUInstPrinter.cpp",
    "AMDGPUMCAsmInfo.cpp",
    "AMDGPUMCCodeEmitter.cpp",
    "AMDGPUMCTargetDesc.cpp",
    "AMDGPUTargetStreamer.cpp",
    "R600MCCodeEmitter.cpp",
    "R600MCTargetDesc.cpp",
    "SIMCCodeEmitter.cpp",
  ]
}
